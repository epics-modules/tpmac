CLOSE

;###############################################
; Define motion for Energy in Accel DCM
; Original Author: Tom Cobb
; Used variables: Q71..Q79, Q81..Q89, Q91..Q99, Q20, P$(PLC)00
; Macros (and example values): 
; COORD = $(COORD) ;CS number (only works for CS 1..9), e.g. 2
; PLC = $(PLC) ;PLC number, should be CS number+15, e.g. 17
; BRAGG = $(BRAGG) ;Axisnum for Bragg, e.g. 2
; T2 = $(T2) ;Axisnum for T2, e.g. 3
;################################################

; Note that we are providing a CS axis with an MRES of 0.001 and EGU the same
; as the underlying axes
; This is needed as the motor record will only move in full steps

; Change to CS$(COORD)
&$(COORD)

; Set relevant axes to use kinematics
#$(BRAGG)->I
#$(T2)->I

; Trig in degrees
i15=0

; These are set by mres_reporting_motor.template
#define BMRES P(100+$(BRAGG))
#define BOFF  P(300+$(BRAGG))
#define TMRES P(100+$(T2))
#define TOFF  P(300+$(T2))

; Setup energy calculation variables
Q110=12.3985 ; Evlambda
Q111=3.1355  ; d = crystal spacing in angstrom

OPEN FORWARD
; This kinematic is for the DCM energy
; need to multiply everything by 10000 to give decent MRES value
; X = Q7 = energy in kEv = {Evlamda}/(2*{d}*sin(Bragg))
; Y = Q8 = offset in mm = 2*(T)*cos(Bragg)
CLEAR
Q20=2*Q111*SIN(BMRES*P$(BRAGG)+BOFF)
IF (Q20>0.01)
    Q7=10000*Q110/(Q20)
    Q8=20000*(TMRES*P$(T2)+TOFF)*COS(BMRES*P$(BRAGG)+BOFF)
ELSE
    M5$(COORD)82=1
ENDIF
CLOSE

OPEN INVERSE
CLEAR
; P$(BRAGG) = Bragg = arcsin({Evlamda}/(2*{d}*(Energy)) or arcsin((wavelength)/(2*{d}))
; P$(T2) = T = (Offset)/2*cos(Bragg)
Q20=10000*Q110/(2*Q111*Q7)
IF (ABS(Q20)<1)
    P$(BRAGG)=(ASIN(Q20)-BOFF)/BMRES
    P$(T2)=(Q8/(20000*COS(ASIN(Q20)))-TOFF)/TMRES
ELSE
    M5$(COORD)82=1
ENDIF
CLOSE

OPEN PLC $(PLC)
; PLC for position reporting
CLEAR
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define BVAL m$(BRAGG)62/(I$(BRAGG)08*32)
#define TVAL m$(T2)62/(I$(T2)08*32)
; Put deadband into Q9x
#define BDB  i$(BRAGG)65*BMRES/16
#define TDB  i$(T2)65*TMRES/16
ADDRESS&$(COORD)
P$(PLC)00=2*Q111*SIN(BMRES*BVAL+BOFF)
IF (P$(PLC)00>0.01)
    Q87=10000*Q110/P$(PLC)00
    Q88=20000*(TMRES*TVAL+TOFF)*COS(BMRES*BVAL+BOFF)
    ; Approximations to deadband as a calculated value is not worth the effort
    Q97=200000*ABS(BDB)
    Q98=100000*(ABS(BDB)+ABS(TDB))
ELSE
    M5$(COORD)82=1
ENDIF
CLOSE
ENABLE PLC $(PLC)


