CLOSE

;################################################
; Define motion for 2 slit Apertures
; Original Author: Tom Cobb
; Used variables: Q71..Q79, Q81..Q89, Q91..Q99
; Macros 
; COORD = $(COORD) ;CS number, e.g. 2
; PLC = $(PLC) ;PLC number, should be CS number+15, e.g. 17
; AP = $(AP) ;Axisnum for Aperture+ (clips the +ve edge of the beam), e.g. 1
; AM = $(AM) ;Axisnum for Aperture- (clips the -ve edge of the beam), e.g. 3
; ASIZE = $(ASIZE) ;Size of Aperture, e.g. 5.85
;################################################

; Note that we are providing a CS axis with an MRES of 0.001 and EGU the same
; as the underlying axes
; This is needed as the motor record will only move in full steps

; Change to CS$(COORD)
&$(COORD)

; Set relevant axes to use kinematics
#$(AM)->I ; +ve Aperture
#$(AP)->I ; -ve Aperture

; These are set by mres_reporting_motor.template
#define AMMRES P(100+$(AM))
#define AMOFF  P(300+$(AM))
#define APMRES P(100+$(AP))
#define APOFF  P(300+$(AP))

OPEN FORWARD
; Calculate Gap and Centre
; X = Q7 = centre in mm = ({A-}+{A+})/2
; Y = Q8 = gap in mm = {ASIZE}-{A-}+{A+} 
; Note that we divide by 0.0001 to give CS mres of 0.0001
; Also note the use of mres and offset in the calculation
CLEAR
Q7=(AMMRES*P$(AM)+AMOFF+APMRES*P$(AP)+APOFF)/0.0002
Q8=($(ASIZE)-AMMRES*P$(AM)-AMOFF+APMRES*P$(AP)+APOFF)/0.0001
CLOSE

OPEN INVERSE
; P$(AM) = {A-} = {centre}+(({ASIZE}-{gap})/2)
; P$(AP) = {A+} = {centre}-(({ASIZE}-{gap})/2)
CLEAR
P$(AM)=((Q7*0.0001+($(ASIZE)-Q8*0.0001)/2)-AMOFF)/AMMRES
P$(AP)=((Q7*0.0001-($(ASIZE)-Q8*0.0001)/2)-APOFF)/APMRES
CLOSE

; A PLC(sx+15) needs to be made to do position reporting
; Readbacks should be in &{axisnum}Q81..89 
OPEN PLC$(PLC)
CLEAR
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define AMVAL m$(AM)62/(I$(AM)08*32)
#define APVAL m$(AP)62/(I$(AP)08*32)
; Put deadband into Q9x
#define AMDB  i$(AM)65*AMMRES/16
#define APDB  i$(AP)65*APMRES/16
ADDRESS&$(COORD)
Q87=(AMMRES*AMVAL+AMOFF+APMRES*APVAL+APOFF)/0.0002
Q88=($(ASIZE)-AMMRES*AMVAL-AMOFF+APMRES*APVAL+APOFF)/0.0001
Q97=(ABS(AMDB) + ABS(APDB))*2/0.0001
Q98=Q97
CLOSE
ENABLE PLC$(PLC)
